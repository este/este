// An example of Firebase security and modeling language.
// https://github.com/firebase/bolt/blob/master/docs/guide.md

// Functions

isSignedIn() { auth != null }
isViewer(uid) { isSignedIn() && auth.uid == uid }

// Types

// github.com/firebase/bolt/blob/master/docs/guide.md#dealing-with-timestamps
type CurrentTimestamp extends Number {
  validate() { this == now }
}

type ShortString extends String {
  validate() { this.length <= 100 }
}

type ShortRequiredString extends String {
  // Required form field with maxLength="100".
  validate() { this.length > 0 && this.length <= 100 }
}

type LongString extends String {
  validate() { this.length <= 1000 }
}

type LongRequiredString extends String {
  validate() { this.length > 0 && this.length <= 1000 }
}

type ExtraLongString extends String {
  validate() { this.length <= 10000 }
}

type ExtraLongRequiredString extends String {
  validate() { this.length > 0 && this.length <= 10000 }
}

type HelloWorld {
  createdAt: CurrentTimestamp
}

type User {
  authenticatedAt: Number,
  displayName: LongString,
  id: ShortRequiredString,
  profileImageURL: LongString,
  provider: LongString
}

type Email {
  email: ShortRequiredString
}

// Paths

path /hello-world is HelloWorld {
  // Anyone can create, read, update. No one can delete.
  create() { true }
  read() { true }
  update() { true }
}

path /users {
  read() { true } // Reading all users allowed for demo.
  index() { ["authenticatedAt"] }
}

path /users/{uid} is User {
  // TODO: Move /users read() { true } here for more secured app.
  write() { isViewer(uid) }
}

path /users-emails/{uid} is Email {
  read() { isViewer(uid) }
  write() { isViewer(uid) }
}
