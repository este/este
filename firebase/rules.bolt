// An example of Firebase security and modeling language.
// https://github.com/firebase/bolt/blob/master/docs/guide.md

// Functions

isSignedIn() { auth != null }
isViewer(uid) { isSignedIn() && auth.uid == uid }
isFriend(uid) { true } // We can limit access to sensitive data easily.

// Types

// github.com/firebase/bolt/blob/master/docs/guide.md#dealing-with-timestamps
type CurrentTimestamp extends Number {
  validate() { this == now }
}

type ShortString extends String {
  validate() { this.length <= 100 }
}

type ShortRequiredString extends String {
  // Required form field with maxLength="100".
  validate() { this.length > 0 && this.length <= 100 }
}

type LongString extends String {
  validate() { this.length <= 1000 }
}

type LongRequiredString extends String {
  validate() { this.length > 0 && this.length <= 1000 }
}

type ExtraLongString extends String {
  validate() { this.length <= 10000 }
}

type ExtraLongRequiredString extends String {
  validate() { this.length > 0 && this.length <= 10000 }
}

type HelloWorld {
  createdAt: CurrentTimestamp,
  text: ShortString
}

type User {
  displayName: LongString,
  id: ShortRequiredString,
  photoURL: LongString,
  validate() { this.id == auth.uid }
}

type UserEmail {
  email: ShortRequiredString
}

type UserPresence {
  authenticatedAt: CurrentTimestamp,
  user: User
}

// Paths

path /hello-world is HelloWorld {
  // Anyone can create, read, update. No one can delete.
  create() { true }
  read() { true }
  update() { true }
}

path /users/{uid} is User {
  read() { isFriend(uid) }
  write() { isViewer(uid) }
}

path /users-emails/{uid} is UserEmail {
  read() { isViewer(uid) }
  write() { isViewer(uid) }
}

path /users-presence {
  read() { true } // Sure we can limit access here as well.
}

path /users-presence/{uid} is UserPresence[] {
  create() { isViewer(uid) }
  update() { isViewer(uid) }
  // Figure out how it can be protected. Waiting for Kato response.
  // Can't be simple isViewer because unauth user connection would live forever.
  delete() { true }
}
